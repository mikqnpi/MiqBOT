plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.4'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
}

sourceSets {
    main {
        proto {
            srcDir '../proto'
            include 'MiqBOT_bridge_v1.proto'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    implementation "com.google.protobuf:protobuf-java:3.25.3"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.3"
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {}
            }
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

processResources {
    inputs.property 'version', project.version
    filesMatching('fabric.mod.json') {
        expand(version: project.version)
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
